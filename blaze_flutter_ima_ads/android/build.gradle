group 'com.blaze.flutterblazeima'
version '1.0-SNAPSHOT'

buildscript {

    ext {
        // Keep only versions that are actually specific to BlazeSDK
        blaze_default_kotlin_version = '2.0.21'
        blazesdk_version = '1.11.6'
        blaze_gson_version = '2.10.1'
        blaze_ima_version = '3.33.0'
    }

    ext.safeExtGet = { prop, fallback ->
        rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }

    ext.blaze_kotlin_version = safeExtGet('kotlin_version', blaze_default_kotlin_version)

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.7.3")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$blaze_kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace 'com.blaze.flutterblazeima'

    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion flutter.minSdkVersion

        consumerProguardFiles "consumer-rules.pro"
    }

    dependencies {
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.mockito:mockito-core:5.0.0'

        implementation 'androidx.appcompat:appcompat:1.6.1'

        implementation group: "com.blaze", name: "blazesdk", version: "$blazesdk_version"
        implementation group: "com.blaze", name: "ima", version: "$blazesdk_version"

        // IMA SDK
        implementation "com.google.ads.interactivemedia.v3:interactivemedia:$blaze_ima_version"

        // Gson.
        implementation "com.google.code.gson:gson:$blaze_gson_version"
        
        // Dependency on main Flutter SDK for AsyncBridge access
        implementation project(':blaze_flutter_sdk')
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
